//file:noinspection SpellCheckingInspection
import io.pzstorm.capsid.setup.xml.LaunchRunConfig
import io.pzstorm.capsid.setup.VmParameter

plugins {
	id 'io.pzstorm.capsid' version '0.3.0'

	// Plugin that keeps your code spotless with Gradle
	// https://plugins.gradle.org/plugin/com.diffplug.spotless
	id "com.diffplug.spotless" version "5.12.1"

	// https://plugins.gradle.org/plugin/net.ltgt.errorprone
	id 'net.ltgt.errorprone' version '2.0.1'
}
apply plugin: 'com.diffplug.spotless'
apply from: "https://gist.githubusercontent.com/yooksi/${spotlessGistID}/raw/spotless.gradle"

version '0.1.0'

capsid {
	isModProject = false
	setProjectRepository('pzstorm', 'storm')
}

dependencies {
	// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'

	// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.1'

	// https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
	implementation 'org.apache.logging.log4j:log4j-core:2.14.0'

	// https://mvnrepository.com/artifact/org.jetbrains/annotations
	compileOnly 'org.jetbrains:annotations:20.1.0'

	// https://mvnrepository.com/artifact/com.google.errorprone/error_prone_core
	errorprone 'com.google.errorprone:error_prone_core:2.6.0'

	// https://mvnrepository.com/artifact/com.google.errorprone/javac
	errorproneJavac('com.google.errorprone:javac:9+181-r4173-1')

	// https://mvnrepository.com/artifact/com.google.guava/guava
	implementation 'com.google.guava:guava:30.1.1-jre'

	// https://mvnrepository.com/artifact/org.ow2.asm/asm
	implementation 'org.ow2.asm:asm:9.1'

	// https://mvnrepository.com/artifact/org.ow2.asm/asm-commons
	implementation 'org.ow2.asm:asm-commons:9.1'

	// https://mvnrepository.com/artifact/org.ow2.asm/asm-tree
	implementation 'org.ow2.asm:asm-tree:9.1'

	// https://mvnrepository.com/artifact/org.ow2.asm/asm-util
	implementation 'org.ow2.asm:asm-util:9.1'

	// https://mvnrepository.com/artifact/org.ow2.asm/asm-analysis
	implementation 'org.ow2.asm:asm-analysis:9.1'
}

test {
	useJUnitPlatform()
	testLogging.events "passed", "skipped", "failed"
}

tasks.createLaunchRunConfigs.configure {
	actions.clear()
	doLast {
		def vmParameterBuilder = new VmParameter.Builder()
		def os = org.gradle.internal.os.OperatingSystem.current()
		if (os == org.gradle.internal.os.OperatingSystem.LINUX) {
			vmParameterBuilder = vmParameterBuilder
					.withJavaLibraryPaths(gameDir as String, "${gameDir}/linux64" as String, "${gameDir}/jre64/lib/amd64" as String)
					.withLwjglLibraryPaths(gameDir)
		}
		//@formatter:off
		LaunchRunConfig launchStorm = new LaunchRunConfig(
				"Launch Storm", "io.pzstorm.storm.StormLauncher",
				vmParameterBuilder.build(), [
						Main: new File(file(gameDir), "logs/storm/main.log").toPath() ,
						Debug: new File(file(gameDir), "logs/storm/debug.log").toPath()] as Map
		)//@formatter:on
		launchStorm.configure(getProject()).writeToFile()
	}
}
